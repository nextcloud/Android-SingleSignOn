/*
 * Nextcloud Android Single-Sign-On Library
 *
 * SPDX-FileCopyrightText: 2017-2024 Nextcloud GmbH and Nextcloud contributors
 * SPDX-License-Identifier: GPL-3.0-or-later
 */


import com.github.spotbugs.snom.Confidence
import com.github.spotbugs.snom.Effort
import com.github.spotbugs.snom.SpotBugsTask
import org.jetbrains.kotlin.gradle.dsl.JvmTarget

buildscript {
    ext {
        junit_version = '4.13.2'
    }
    dependencies {
        classpath 'com.github.spotbugs.snom:spotbugs-gradle-plugin:6.4.8'
    }
}

plugins {
    id "com.diffplug.spotless" version "8.1.0"
}

apply plugin: "kotlin-android"
apply plugin: 'com.android.library'
apply plugin: "com.github.spotbugs"
apply plugin: "io.gitlab.arturbosch.detekt"
apply plugin: 'maven-publish' // needed for JitPack.io

group = 'com.github.nextcloud'

spotbugs {
    ignoreFailures = true // should continue checking
    effort = Effort.MAX
    reportLevel = Confidence.valueOf('MEDIUM')
}

configurations {
    ktlint
}

android {
    namespace = 'com.nextcloud.android.sso'

    defaultConfig {
        minSdk 21
        targetSdk = 36
        compileSdk = 36
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    buildFeatures {
        aidl = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }

    productFlavors {}

    lint {
        abortOnError = false
        disable 'MissingTranslation', 'ExtraTranslation', 'MissingQuantity', 'InconsistentArrays', 'TypographyEllipsis', 'GradleDependency', 'VectorPath', 'IconMissingDensityFolder', 'IconDensities'
        htmlOutput = file("$project.layout.buildDirectory/reports/lint/lint.html")
        htmlReport = true
    }
}

tasks.withType(SpotBugsTask) { task ->
    String variantNameCap = task.name.replace("spotbugs", "")
    String variantName = variantNameCap.substring(0, 1).toLowerCase() + variantNameCap.substring(1)
    dependsOn "compile${variantNameCap}Sources"

    classes = files("$project.buildDir/intermediates/javac/${variantName}")
    excludeFilter = file("${project.rootDir}/spotbugs-filter.xml")
    reports {
        xml {
            required = true
        }
        html {
            required = true
            outputLocation = file("$project.buildDir/reports/spotbugs/spotbugs.html")
            stylesheet = 'fancy.xsl'
        }
    }
}

tasks.register('ktlint', JavaExec) {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "--reporter=plain", "--reporter=plain,output=${project.layout.buildDirectory}/ktlint.txt,src/**/*.kt"
}

detekt {
    reports {
        xml {
            enabled = false
        }
    }
    config = files("detekt.yml")
    input = files("src/")
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    implementation "androidx.appcompat:appcompat:1.7.1"
    implementation 'androidx.annotation:annotation:1.9.1'
    implementation 'androidx.core:core:1.17.0'
    implementation 'androidx.fragment:fragment:1.8.9'

    implementation 'com.google.android.material:material:1.13.0'

    api 'com.google.code.gson:gson:2.13.2'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava3:rxjava:3.1.12'

    implementation 'commons-io:commons-io:2.21.0'

    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'

    spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.14.0'
    spotbugsPlugins 'com.mebigfatguy.fb-contrib:fb-contrib:7.7.4'

    ktlint "com.pinterest.ktlint:ktlint-cli:1.8.0"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.21.0'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release

                groupId = 'com.nextcloud.android.sso'
                artifactId = 'master'
            }
        }
    }
}

spotless {
    kotlin {
        target "**/*.kt"
        ktlint()
    }
}
